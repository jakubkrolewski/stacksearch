ext {
    versions = [
            butterknife: "8.2.1",
            dagger: "2.5",
            guava: "18.0",
            retrofit: "2.1.0",
            rxjava: "1.1.7",
            support: "24.1.1"
    ]
}

buildscript {
    configurations {
        //noinspection GroovyAssignabilityCheck
        all {
            resolutionStrategy {
                force "com.android.support:support-annotations:$versions.support"
                force "com.google.guava:guava:$versions.guava"
            }
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        debug {
            storeFile file("${rootDir}/debug.keystore")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " DEBUG"
            signingConfig signingConfigs.debug
        }
        debugProguard.initWith(buildTypes.debug)
        debugProguard {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), fileTree('proguard/rules_debug')
            versionNameSuffix " DEBUG PROGUARD"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    apt "com.google.dagger:dagger-compiler:$versions.dagger"
    apt "com.jakewharton:butterknife-compiler:$versions.butterknife"

    compile "com.android.support:appcompat-v7:$versions.support"
    compile "com.android.support:cardview-v7:$versions.support"
    compile "com.android.support:design:$versions.support"
    compile "com.android.support:recyclerview-v7:$versions.support"
    compile "com.artemzin.rxjava:proguard-rules:$versions.rxjava.0"
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile "com.google.dagger:dagger:$versions.dagger"
    compile "com.google.guava:guava:$versions.guava"
    compile "com.jakewharton:butterknife:$versions.butterknife"
    compile "com.jakewharton.timber:timber:4.1.2"
    compile "com.squareup.okhttp3:logging-interceptor:3.4.1"
    compile "com.squareup.retrofit2:adapter-rxjava:$versions.retrofit"
    compile "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    compile "com.squareup.retrofit2:retrofit:$versions.retrofit"
    compile "info.android15.nucleus:nucleus-support-v7:4.0.0"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "io.reactivex:rxjava:$versions.rxjava"

    provided "org.projectlombok:lombok:1.16.10"

    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:robolectric:3.1.1"
}
